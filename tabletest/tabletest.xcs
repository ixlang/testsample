// xlang

class MainDialog : QXDialog
{

    QXPushButton btnOk;
    QXTableWidget tableWidget;
    QXPushButton combtn;
    public bool create()override
    {
        QXBuffer qb = new QXBuffer();
        byte [] buffer = __xPackageResource("ui/dialog.ui");
        qb.setBuffer(buffer, 0, buffer.length);

        if (load(qb)) {
            btnOk = (QXPushButton)attachByName(new QXPushButton(), "btnOk");
            tableWidget = (QXTableWidget)attachByName(new QXTableWidget(), "tableWidget");

            tableWidget.setColumnCount(8);
            tableWidget.setRowCount(10);

            String []columns = {"项目1", "项目2", "项目3", "项目4", "项目5", "项目6", "项目7", "项目8"};
            tableWidget.setHHColumns(columns);

            String []vcolumns = {"条目1", "条目2", "条目3", "条目4", "条目5", "条目6", "条目7", "条目8"};
            tableWidget.setVHColumns(vcolumns);
            String [] items = {"C++", "java", "c#"};

            for (int i = 0; i < 10; i ++) {
                for (int a = 0; a < 8; a ++) {
                    if (a == 1) {
                        QXComboBox comb = new QXComboBox();
                        comb.create();
                        comb.addItems(items);
                        tableWidget.setCellWidget(i,a,comb);
                    } else {
                        if (a == 7) {
                            QXPushButton comb = new QXPushButton();
                            if (combtn == nilptr){
                                combtn = comb;
                            }
                            comb.create();
                            comb.setText("click");
                            tableWidget.setCellWidget(i,a,comb);

                            comb.setOnClickListener(new onClickListener() {
                                int x = i, y = a;
                                void onClick(QXObject obj,bool checked)override {
                                    QXMessageBox.Critical("注意","你点击了:" + x + ":" + y, QXMessageBox.Ok,QXMessageBox.Ok);
                                }
                            });
                        } else {
                            tableWidget.setItem(i,a,nilptr,"x" + i + "y" + a);
                        }
                    }

                    long item = tableWidget.getItem(i,a);
                    if (a == 0) {
                        tableWidget.setItemCheck(item,false);
                    }
                    if (a == 2) {
                        tableWidget.setItemColor(item,0xffff0000);
                    }
                    if (a == 3) {
                        tableWidget.setItemBackColor(item,0xcc00cc00);
                    }
                    if (a == 4) {
                        tableWidget.modifyItemFlags(item,0,2);
                    }
                }
            }

            btnOk.setOnClickListener(new onClickListener() {
                void onClick(QXObject obj, bool checked)override {
                    tableWidget.clear();
                    QXMessageBox.Information("info", " Hello world!", QXMessageBox.Ok, QXMessageBox.Ok);
                    combtn.setText("sad");
                }
            });

            return true;
        }
        return false;
    }
};


class XMyApp : QXApplication
{

    MainDialog dialog;

    public bool create()
    {
        if (super.createQXApplication()) {
            dialog = new MainDialog();
            if (dialog.create()) {
                dialog.show();
                return true;
            }
        }
        return false;
    }

};


int main(String [] args)
{
    XMyApp app = new XMyApp();
    if (app.create()) {
        app.run();
    }
    return 0;
}